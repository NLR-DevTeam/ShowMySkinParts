pluginManagement {
    repositories {
        maven {
            name 'Fabric'
            url 'https://maven.fabricmc.net'
        }

        maven {
            name 'JitPack'
            url 'https://jitpack.io'
        }

        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }

    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == 'com.replaymod.preprocess') {
                // Thanks @Hendrix-Shen
                useModule("com.github.Hendrix-Shen:preprocessor:${requested.version}")
            }
        }
    }
}

def versions = [
        '1.20.4', '1.20.6',
        '1.21.1'
]

for (String version : versions) {
    include(":$version")
    def proj = project(":$version")
    proj.projectDir = file("versions/$version")
    proj.buildFileName = '../1.21.1/build.gradle'
}

include(':fabricWrapper')
